num = @{ "-"? ~ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? }
var = @{ (ASCII_ALPHANUMERIC | "_")+ }
sym = @{ (!(ASCII_ALPHANUMERIC | WHITESPACE | "(" | ")" | "\n" | "_"  | "." | "|" | "," | "=" | ":" | "$") ~ ANY)+ }
atom = _{ num | sym | var | paren }

tuple = { "," }
union = { "|" }
dot = { "." }
def = { "=" }
op = _{ def | tuple | union | dot | sym | var }

app = { "" }
close_paren = { ")" }
paren = _{ "(" ~ line ~ close_paren }

node_a = _{ atom ~ (op ~ atom)* ~ op ~ indent ~ line ~ dedent }
node_b = _{ atom ~ (op ~ atom)* ~ app ~ indent ~ line ~ dedent }
node_c = _{ (node_a | node_d) ~ indent ~ op ~ atom ~ (samedent ~ op ~ atom)* ~ dedent }
node_d = _{ atom ~ (op ~ atom | app ~ atom)* }
line = { node_a | node_c | node_b | node_d }

prog = _{ SOI ~ line ~ "\n"* ~ EOI }

indent = @{ "\n"+ ~ PEEK[..] ~ PUSH(WHITESPACE+) }
dedent = @{ EOI | "\n"+ ~ !PEEK[..] ~ DROP }
samedent = @{ "\n"+ ~ PEEK[..] }

WHITESPACE = _{ " " }
