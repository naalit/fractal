keyword = @{ "fun" | "do" | "rec" }
num = @{ "-"? ~ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? }
var = @{ !num ~ !keyword ~ (ASCII_ALPHANUMERIC | "_")+ }
sym = @{ (!(ASCII_ALPHANUMERIC | WHITESPACE | "(" | ")" | "\n" | "_"  | "." | "|" | "," | "=" | ":" | "$") ~ ANY)+ }
nil = { "(" ~ ")" }
rec = { "rec" ~ var }
atom = _{ nil | rec | num | sym | var | paren }

tuple = { "," }
union = { "|" }
inter = { ":" }
dot   = { "." }
def   = { !("=>") ~ "=" }
op = _{ tuple | union | inter | dot | sym | var }

app = { "" }
close_paren = { ")" }
paren = _{ "(" ~ line ~ close_paren }

block = { "do" ~ indent ~ line ~ (samedent ~ line)* ~ dedent }

expr = _{ atom | indent ~ line ~ dedent }

node_a = _{ atom ~ (op ~ atom)* ~ op ~ indent ~ line ~ dedent }
node_b = _{ atom ~ (op ~ atom)* ~ app ~ indent ~ line ~ dedent }
node_c = _{ (node_a | node_d) ~ indent ~ op ~ expr ~ (samedent ~ op ~ expr)* ~ dedent }
node_d = _{ atom ~ (op ~ atom | app ~ atom)* }
node_e = _{ atom ~ (op | app) ~ (atom ~ (op | app))* ~ block }
line = { block | definition | fun | node_e | node_a | node_c | node_b | node_d }

definition = _{ node_d ~ (def ~ line
	| indent ~ def ~ line ~ dedent
  | def ~ indent ~ line ~ dedent
  | indent ~ def ~ samedent ~ line ~ dedent ~ dedent) }

fun_pat = { node_d }
fun_arm = { fun_pat ~ "=>" ~ line }
fun = { "fun" ~ (fun_arm | indent ~ fun_arm ~ (samedent ~ fun_arm)* ~ dedent) }

prog = _{ SOI ~ "\n"* ~ line ~ ("\n"+ ~ line)* ~ "\n"* ~ EOI }

blank = @{ "\n" | WHITESPACE* ~ COMMENT ~ "\n" }
indent = @{ blank+ ~ PEEK[..] ~ PUSH(WHITESPACE+) }
dedent = @{ ((WHITESPACE | blank)* ~ EOI) | &(blank+ ~ !PEEK[..]) ~ DROP }
samedent = @{ blank+ ~ PEEK[..] }

WHITESPACE = _{ " " }
COMMENT = _{ "#" ~ (!"\n" ~ ANY)* }
